apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply plugin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdk
    defaultConfig {
        applicationId "com.tiny.demo.firstlinecode"
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // dex突破65535的限制
        multiDexEnabled true

        //为flavor设置一个版本，命名是随意的
        flavorDimensions "flc"

        //多渠道打包的配置，除了下面的，如果没有这句代码的话，apk的输出位置会在build/outputs/apk下面
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wap"]

        //lambda
//        jackOptions {
//            enabled true
//        }
        android.compileOptions.sourceCompatibility 1.8
        android.compileOptions.targetCompatibility 1.8

        //vector to svg, and need
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        //存储在根目录的gradle.properties文件中，更安全。
        debug {
            storeFile file(KEY_PATH)
            storePassword KEY_PASS
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASS
        }

        release {
            storeFile file(KEY_PATH)
            storePassword KEY_PASS
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASS
        }
    }

    //lambda
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //data binding
    dataBinding {
        enabled = true
    }

    buildTypes {
//        debug {
//            // 显示Log
//            buildConfigField "boolean", "LOG_DEBUG", "false"
//            minifyEnabled false
//            zipAlignEnabled false
//            shrinkResources false
//
//            //给测试版本的appid添加后缀。
////            applicationIdSuffix ".debug"
//        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
//            versionNameSuffix "_release"
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            //打包好的文件进行输出，修改文件名称的操作在下面进行。
            applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName.endsWith('.apk')) {
                        // 输出apk名称为flc_v1.0_2015-01-15_wandoujia.apk
                        outputFileName = "first_line_code_v${defaultConfig.versionName}_${variant.productFlavors[0].name}_${getGitRevision()}.apk"
                    }
                }
            }
        }
    }

    greendao {
        schemaVersion 1//数据库版本号
        daoPackage 'com.com.sky.downloader.greendao'//设置DaoMaster、DaoSession、Dao包名
        targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
        //targetGenDirTest：设置生成单元测试目录
        //generateTests：设置自动生成单元测试用例
    }

    // 友盟多渠道打包
    productFlavors {
        //渠道名的闭包中可以覆写defaultConfig中的任何一个属性。
        official {
            //这里也可以配置每个渠道的appid
            minSdkVersion rootProject.ext.android.minLimitSdk
        }
        baidu {
            applicationId "com.tiny.demo.firstlinecode.baidu"
            minSdkVersion rootProject.ext.android.minLimitSdk
        }
        xiaomi {
            applicationId "com.tiny.demo.firstlinecode.xiaomi"
            minSdkVersion rootProject.ext.android.minLimitSdk
        }
        wandoujia {}
        _360 {}
        tencent {}
        huawei {}
        anzhi {}
        website {}
        wap {}
        vivo {}
        update {}
        _test {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = ["UMENG_CHANNEL_VALUE": name]
    }

    //针对2.3版本添加的配置
    lintOptions {
        checkReleaseBuilds false
        //Or, if you prefer, you can continue to check fr errors in release builds,
        //but continue the build even when errors are foundsL
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def getGitRevision() {
    return "git rev-parse --short HEAD".execute().text.trim()
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.dependencies.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api project(':pulltorefresh-library')

    //解决冲突
    androidTestImplementation "com.android.support:support-annotations:$rootProject.ext.dependencies.supportLibraryVersion"

    // Android JUnit Runner

    //noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    // JUnit4 Rules
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // Espresso core
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.dependencies.espresso"

    //Espresso-Intents只支持 Espresso 2.1+和0.3以上版本的test包以上
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.ext.dependencies.espresso"

    //junit
    testImplementation "junit:junit:$rootProject.ext.dependencies.junit"

    //support library
    api "com.android.support:appcompat-v7:$rootProject.ext.dependencies.supportLibraryVersion"
    api "com.android.support:design:$rootProject.ext.dependencies.supportLibraryVersion"
    api "com.android.support:support-v4:$rootProject.ext.dependencies.supportLibraryVersion"
    api "com.android.support:recyclerview-v7:$rootProject.ext.dependencies.supportLibraryVersion"
    api "com.android.support:cardview-v7:$rootProject.ext.dependencies.supportLibraryVersion"

    //26.1.0 was deprecated
    api "com.android.support:percent:$rootProject.ext.dependencies.supportPercentVersion"

    //gson
    api "com.google.code.gson:gson:$rootProject.ext.dependencies.gson"

    //stetho
    api "com.facebook.stetho:stetho:$rootProject.ext.dependencies.stetho"
    api "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.dependencies.stetho"

    //retrofit2 + rxjava2
    api "com.squareup.retrofit2:retrofit:$rootProject.ext.dependencies.retrofit"
    api "com.squareup.retrofit2:converter-gson:$rootProject.ext.dependencies.retrofit"
    api "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.dependencies.retrofit"
    api 'io.reactivex.rxjava2:rxjava:2.1.16'
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'

    //okhttp
    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    //guava
    api "com.google.guava:guava:$rootProject.ext.dependencies.guavaVersion"

    //butterknife
    api "com.jakewharton:butterknife:$rootProject.ext.dependencies.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.dependencies.butterknife"

    //litepal
    implementation 'org.litepal.android:java:3.0.0'

    //circle image view
    api 'de.hdodenhof:circleimageview:2.2.0'

    //glide
    api 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'

    //constraint-layout
    api 'com.android.support.constraint:constraint-layout:1.1.3'

    //nineoldandroid
    api 'com.nineoldandroids:library:2.4.0'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.dependencies.leakcanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.dependencies.leakcanary"
    // Optional, if you use support library fragments:
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$rootProject.ext.dependencies.leakcanary"

    //BaseRecyclerViewAdapterHelper相关
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.31'
    api 'com.kyleduo.switchbutton:library:2.0.0'
    api 'com.orhanobut:logger:2.1.1'
    api project(':material-spinner-1.0.5')

    //SmartRefreshLayout相关

    //稳定版

    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-31'
    api 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-31'//没有使用特殊Header，可以不加这行
//    api 'com.android.support:appcompat-v7:25.3.1'//版本 23以上（必须）


    api 'com.github.czy1121:loadinglayout:1.0.1'
    api 'com.wang.avi:library:2.1.3'

    //SmartShow
    implementation('com.github.the-pig-of-jungle.smart-show:all:2.7.7') {
        exclude group: 'com.android.support'
    }

    //    compile 'com.lzy.net:okgo:2.1.4'//old
    api 'com.lzy.net:okgo:3.0.4'
    api 'com.zhy:autolayout:1.4.5'
    api 'com.gzsll.jsbridge:library:1.1.0'
    api project(':tinymodule')
    api 'com.squareup:otto:1.3.8'
    api 'org.greenrobot:eventbus:3.1.1'
    api 'com.google.dagger:dagger:2.4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.4'
    api 'com.youth.banner:banner:1.4.10'

    //banner
    api 'com.bigkoo:convenientbanner:2.0.5'
    api 'com.github.lsjwzh.RecyclerViewPager:lib:v1.1.2@aar'

    //仿照CardView效果
    api 'com.loopeer.lib:shadow:0.0.4-beta2'

    //commons-lang3
    api 'org.apache.commons:commons-lang3:3.7'

    //greendao
    api 'org.greenrobot:greendao:3.2.2' // add library

    api 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
//    api 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.1.1'
    api 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    api 'com.jakewharton.rxbinding2:rxbinding-design:2.1.1'
    api 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.1.1'
    api 'com.jakewharton.rxbinding2:rxbinding-leanback-v17:2.1.1'

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.anko:anko-common:$anko_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
//    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

}

kotlin {
    experimental {
        coroutines "enable"
    }
}
